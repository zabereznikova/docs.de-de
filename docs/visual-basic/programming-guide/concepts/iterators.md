---
title: Iterators
ms.date: 07/20/2015
ms.assetid: f26b5c1e-fe9d-4004-b287-da7919d717ae
ms.openlocfilehash: e638d35aeb86837d91fb14681d300772e3c2375a
ms.sourcegitcommit: f8c270376ed905f6a8896ce0fe25b4f4b38ff498
ms.translationtype: MT
ms.contentlocale: de-DE
ms.lasthandoff: 06/04/2020
ms.locfileid: "84410928"
---
# <a name="iterators-visual-basic"></a><span data-ttu-id="56b45-102">Iteratoren (Visual Basic)</span><span class="sxs-lookup"><span data-stu-id="56b45-102">Iterators (Visual Basic)</span></span>

<span data-ttu-id="56b45-103">Ein *Iterator* kann verwendet werden, um Auflistungen wie Listen und Arrays schrittweise durchzugehen.</span><span class="sxs-lookup"><span data-stu-id="56b45-103">An *iterator* can be used to step through collections such as lists and arrays.</span></span>

<span data-ttu-id="56b45-104">Eine Iteratormethode oder eine `get`-Zugriffsmethode führt eine benutzerdefinierte Iteration einer Auflistung durch.</span><span class="sxs-lookup"><span data-stu-id="56b45-104">An iterator method or `get` accessor performs a custom iteration over a collection.</span></span> <span data-ttu-id="56b45-105">Eine Iteratormethode verwendet die [Yield](../../language-reference/statements/yield-statement.md) -Anweisung, um jedes Element einzeln zurückzugeben.</span><span class="sxs-lookup"><span data-stu-id="56b45-105">An iterator method uses the [Yield](../../language-reference/statements/yield-statement.md) statement to return each element one at a time.</span></span> <span data-ttu-id="56b45-106">Wenn eine `Yield`-Anweisung erreicht wird, wird die aktuelle Position im Code gespeichert.</span><span class="sxs-lookup"><span data-stu-id="56b45-106">When a `Yield` statement is reached, the current location in code is remembered.</span></span> <span data-ttu-id="56b45-107">Wenn die Iteratorfunktion das nächste Mal aufgerufen wird, wird die Ausführung von dieser Position neu gestartet.</span><span class="sxs-lookup"><span data-stu-id="56b45-107">Execution is restarted from that location the next time the iterator function is called.</span></span>

<span data-ttu-id="56b45-108">Sie verwenden einen Iterator aus Client Code, indem Sie [für jede... Next](../../language-reference/statements/for-each-next-statement.md) -Anweisung oder mithilfe einer LINQ-Abfrage.</span><span class="sxs-lookup"><span data-stu-id="56b45-108">You consume an iterator from client code by using a [For Each…Next](../../language-reference/statements/for-each-next-statement.md) statement, or by using a LINQ query.</span></span>

<span data-ttu-id="56b45-109">In folgendem Beispiel führt die erste Iteration der `For Each`-Schleife dazu, dass die Ausführung solange in der Iteratormethode `SomeNumbers` fortschreitet, bis der ersten `Yield`-Anweisung erreicht wird.</span><span class="sxs-lookup"><span data-stu-id="56b45-109">In the following example, the first iteration of the `For Each` loop causes execution to proceed  in the `SomeNumbers` iterator method until the first `Yield` statement is reached.</span></span> <span data-ttu-id="56b45-110">Diese Iteration gibt den Wert 3 zurück, und die aktuelle Postion in der Iteratormethode wird beibehalten.</span><span class="sxs-lookup"><span data-stu-id="56b45-110">This iteration returns a value of 3, and the current location in the iterator method is retained.</span></span> <span data-ttu-id="56b45-111">In der nächsten Iteration der Schleife wird die Ausführung in der Iteratormethode da fortgesetzt, wo sie beendet wurde, und endet dann wieder an einem `Yield`-Ausdruck.</span><span class="sxs-lookup"><span data-stu-id="56b45-111">On the next iteration of the loop, execution in the iterator method continues from where it left off, again stopping when it reaches a `Yield` statement.</span></span> <span data-ttu-id="56b45-112">Diese Iteration gibt den Wert 5 zurück, und die aktuelle Postion in der Iteratormethode wird beibehalten.</span><span class="sxs-lookup"><span data-stu-id="56b45-112">This iteration returns a value of 5, and the current location in the iterator method is again retained.</span></span> <span data-ttu-id="56b45-113">Die Schleife wird beendet, wenn das Ende der Iteratormethode erreicht wird.</span><span class="sxs-lookup"><span data-stu-id="56b45-113">The loop completes when the end of the iterator method is reached.</span></span>

```vb
Sub Main()
    For Each number As Integer In SomeNumbers()
        Console.Write(number & " ")
    Next
    ' Output: 3 5 8
    Console.ReadKey()
End Sub

Private Iterator Function SomeNumbers() As System.Collections.IEnumerable
    Yield 3
    Yield 5
    Yield 8
End Function
```

<span data-ttu-id="56b45-114">Der Rückgabetyp einer Iteratormethode oder einer `get`-Zugriffsmethode kann <xref:System.Collections.IEnumerable>, <xref:System.Collections.Generic.IEnumerable%601>, <xref:System.Collections.IEnumerator> oder <xref:System.Collections.Generic.IEnumerator%601> sein.</span><span class="sxs-lookup"><span data-stu-id="56b45-114">The return type of an iterator method or `get` accessor can be <xref:System.Collections.IEnumerable>, <xref:System.Collections.Generic.IEnumerable%601>, <xref:System.Collections.IEnumerator>, or <xref:System.Collections.Generic.IEnumerator%601>.</span></span>

<span data-ttu-id="56b45-115">Sie können eine- `Exit Function` oder- `Return` Anweisung verwenden, um die Iterationen zu beenden.</span><span class="sxs-lookup"><span data-stu-id="56b45-115">You can use an `Exit Function` or `Return` statement to end the iteration.</span></span>

<span data-ttu-id="56b45-116">Eine Visual Basic Iteratorfunktion oder `get` Accessor-Deklaration enthält einen [iteratormodifizierer](../../language-reference/modifiers/iterator.md) .</span><span class="sxs-lookup"><span data-stu-id="56b45-116">A Visual Basic iterator function or `get` accessor declaration includes an [Iterator](../../language-reference/modifiers/iterator.md) modifier.</span></span>

<span data-ttu-id="56b45-117">Iteratoren wurden in Visual Basic in Visual Studio 2012 eingeführt.</span><span class="sxs-lookup"><span data-stu-id="56b45-117">Iterators were introduced in Visual Basic in Visual Studio 2012.</span></span>

<span data-ttu-id="56b45-118">**Inhalt**</span><span class="sxs-lookup"><span data-stu-id="56b45-118">**In this topic**</span></span>

- [<span data-ttu-id="56b45-119">Einfacher Iterator</span><span class="sxs-lookup"><span data-stu-id="56b45-119">Simple Iterator</span></span>](#BKMK_SimpleIterator)

- [<span data-ttu-id="56b45-120">Erstellen einer Auflistungsklasse</span><span class="sxs-lookup"><span data-stu-id="56b45-120">Creating a Collection Class</span></span>](#BKMK_CollectionClass)

- [<span data-ttu-id="56b45-121">Try-Blöcke</span><span class="sxs-lookup"><span data-stu-id="56b45-121">Try Blocks</span></span>](#BKMK_TryBlocks)

- [<span data-ttu-id="56b45-122">Anonyme Methoden</span><span class="sxs-lookup"><span data-stu-id="56b45-122">Anonymous Methods</span></span>](#BKMK_AnonymousMethods)

- [<span data-ttu-id="56b45-123">Verwenden von Iteratoren mit einer generischen Liste</span><span class="sxs-lookup"><span data-stu-id="56b45-123">Using Iterators with a Generic List</span></span>](#BKMK_GenericList)

- [<span data-ttu-id="56b45-124">Syntaxinformationen</span><span class="sxs-lookup"><span data-stu-id="56b45-124">Syntax Information</span></span>](#BKMK_SyntaxInformation)

- [<span data-ttu-id="56b45-125">Technische Implementierung</span><span class="sxs-lookup"><span data-stu-id="56b45-125">Technical Implementation</span></span>](#BKMK_Technical)

- [<span data-ttu-id="56b45-126">Verwendung von Iteratoren</span><span class="sxs-lookup"><span data-stu-id="56b45-126">Use of Iterators</span></span>](#BKMK_UseOfIterators)

> [!NOTE]
> <span data-ttu-id="56b45-127">Fügen Sie für alle Beispiele im Thema mit Ausnahme des einfachen iteratorbeispiels [Import](../../language-reference/statements/imports-statement-net-namespace-and-type.md) -Anweisungen für `System.Collections` die `System.Collections.Generic` Namespaces und hinzu.</span><span class="sxs-lookup"><span data-stu-id="56b45-127">For all examples in the topic except the Simple Iterator example, include [Imports](../../language-reference/statements/imports-statement-net-namespace-and-type.md) statements for the `System.Collections` and `System.Collections.Generic` namespaces.</span></span>

## <a name="simple-iterator"></a><a name="BKMK_SimpleIterator"></a><span data-ttu-id="56b45-128">Einfacher Iterator</span><span class="sxs-lookup"><span data-stu-id="56b45-128">Simple Iterator</span></span>

<span data-ttu-id="56b45-129">Im folgenden Beispiel wird eine einzelne- `Yield` Anweisung in einer [for... Nächste](../../language-reference/statements/for-next-statement.md) Schleife.</span><span class="sxs-lookup"><span data-stu-id="56b45-129">The following example has a single `Yield` statement that is inside a [For…Next](../../language-reference/statements/for-next-statement.md) loop.</span></span> <span data-ttu-id="56b45-130">In der `Main`-Methode erstellt jede Iteration des `For Each`-Anweisungstexts einen Aufruf der Iteratorfunktion, die zur nächsten `Yield`-Anweisung übergeht.</span><span class="sxs-lookup"><span data-stu-id="56b45-130">In `Main`, each iteration of the `For Each` statement body creates a call to the iterator function, which proceeds to the next `Yield` statement.</span></span>

```vb
Sub Main()
    For Each number As Integer In EvenSequence(5, 18)
        Console.Write(number & " ")
    Next
    ' Output: 6 8 10 12 14 16 18
    Console.ReadKey()
End Sub

Private Iterator Function EvenSequence(
ByVal firstNumber As Integer, ByVal lastNumber As Integer) _
As System.Collections.Generic.IEnumerable(Of Integer)

    ' Yield even numbers in the range.
    For number As Integer = firstNumber To lastNumber
        If number Mod 2 = 0 Then
            Yield number
        End If
    Next
End Function
```

## <a name="creating-a-collection-class"></a><a name="BKMK_CollectionClass"></a><span data-ttu-id="56b45-131">Erstellen einer Sammlungsklasse</span><span class="sxs-lookup"><span data-stu-id="56b45-131">Creating a Collection Class</span></span>

<span data-ttu-id="56b45-132">In folgendem Beispiel implementiert die `DaysOfTheWeek`-Klasse die <xref:System.Collections.IEnumerable>-Schnittstelle, die eine <xref:System.Collections.IEnumerable.GetEnumerator%2A>-Methode erfordert.</span><span class="sxs-lookup"><span data-stu-id="56b45-132">In the following example, the `DaysOfTheWeek` class implements the <xref:System.Collections.IEnumerable> interface, which requires a <xref:System.Collections.IEnumerable.GetEnumerator%2A> method.</span></span> <span data-ttu-id="56b45-133">Der Compiler ruft die Methode `GetEnumerator` implizit auf, die <xref:System.Collections.IEnumerator> zurückgibt.</span><span class="sxs-lookup"><span data-stu-id="56b45-133">The compiler implicitly calls the `GetEnumerator` method, which returns an <xref:System.Collections.IEnumerator>.</span></span>

<span data-ttu-id="56b45-134">Die `GetEnumerator` -Methode gibt jede Zeichenfolge einzeln mithilfe der `Yield` -Anweisung zurück, und ein- `Iterator` Modifizierer ist in der Funktionsdeklaration.</span><span class="sxs-lookup"><span data-stu-id="56b45-134">The `GetEnumerator` method returns each string one at a time by using the `Yield` statement, and  an `Iterator` modifier is in the function declaration.</span></span>

```vb
Sub Main()
    Dim days As New DaysOfTheWeek()
    For Each day As String In days
        Console.Write(day & " ")
    Next
    ' Output: Sun Mon Tue Wed Thu Fri Sat
    Console.ReadKey()
End Sub

Private Class DaysOfTheWeek
    Implements IEnumerable

    Public days =
        New String() {"Sun", "Mon", "Tue", "Wed", "Thu", "Fri", "Sat"}

    Public Iterator Function GetEnumerator() As IEnumerator _
        Implements IEnumerable.GetEnumerator

        ' Yield each day of the week.
        For i As Integer = 0 To days.Length - 1
            Yield days(i)
        Next
    End Function
End Class
```

<span data-ttu-id="56b45-135">Im folgenden Beispiel wird eine `Zoo`-Klasse erstellt, die eine Auflistung von Tieren enthält.</span><span class="sxs-lookup"><span data-stu-id="56b45-135">The following example creates a `Zoo` class that contains a collection of animals.</span></span>

<span data-ttu-id="56b45-136">Die Anweisung `For Each`, die auf die Instanz der Klasse verweist (`theZoo`), ruft die Methode `GetEnumerator` implizit auf.</span><span class="sxs-lookup"><span data-stu-id="56b45-136">The `For Each` statement that refers to the class instance (`theZoo`) implicitly calls the `GetEnumerator` method.</span></span> <span data-ttu-id="56b45-137">Die Anweisung `For Each`, die auf die Eigenschaften `Birds` und `Mammals` verweist, verwenden die Iteratormethode `AnimalsForType`.</span><span class="sxs-lookup"><span data-stu-id="56b45-137">The `For Each` statements that refer to the `Birds` and `Mammals` properties use the `AnimalsForType` named iterator method.</span></span>

```vb
Sub Main()
    Dim theZoo As New Zoo()

    theZoo.AddMammal("Whale")
    theZoo.AddMammal("Rhinoceros")
    theZoo.AddBird("Penguin")
    theZoo.AddBird("Warbler")

    For Each name As String In theZoo
        Console.Write(name & " ")
    Next
    Console.WriteLine()
    ' Output: Whale Rhinoceros Penguin Warbler

    For Each name As String In theZoo.Birds
        Console.Write(name & " ")
    Next
    Console.WriteLine()
    ' Output: Penguin Warbler

    For Each name As String In theZoo.Mammals
        Console.Write(name & " ")
    Next
    Console.WriteLine()
    ' Output: Whale Rhinoceros

    Console.ReadKey()
End Sub

Public Class Zoo
    Implements IEnumerable

    ' Private members.
    Private animals As New List(Of Animal)

    ' Public methods.
    Public Sub AddMammal(ByVal name As String)
        animals.Add(New Animal With {.Name = name, .Type = Animal.TypeEnum.Mammal})
    End Sub

    Public Sub AddBird(ByVal name As String)
        animals.Add(New Animal With {.Name = name, .Type = Animal.TypeEnum.Bird})
    End Sub

    Public Iterator Function GetEnumerator() As IEnumerator _
        Implements IEnumerable.GetEnumerator

        For Each theAnimal As Animal In animals
            Yield theAnimal.Name
        Next
    End Function

    ' Public members.
    Public ReadOnly Property Mammals As IEnumerable
        Get
            Return AnimalsForType(Animal.TypeEnum.Mammal)
        End Get
    End Property

    Public ReadOnly Property Birds As IEnumerable
        Get
            Return AnimalsForType(Animal.TypeEnum.Bird)
        End Get
    End Property

    ' Private methods.
    Private Iterator Function AnimalsForType( _
    ByVal type As Animal.TypeEnum) As IEnumerable
        For Each theAnimal As Animal In animals
            If (theAnimal.Type = type) Then
                Yield theAnimal.Name
            End If
        Next
    End Function

    ' Private class.
    Private Class Animal
        Public Enum TypeEnum
            Bird
            Mammal
        End Enum

        Public Property Name As String
        Public Property Type As TypeEnum
    End Class
End Class
```

## <a name="try-blocks"></a><a name="BKMK_TryBlocks"></a><span data-ttu-id="56b45-138">Try-Blöcke</span><span class="sxs-lookup"><span data-stu-id="56b45-138">Try Blocks</span></span>

<span data-ttu-id="56b45-139">Visual Basic lässt eine- `Yield` Anweisung im- `Try` Block eines [try... Catch... Abschließend-Anweisung](../../language-reference/statements/try-catch-finally-statement.md).</span><span class="sxs-lookup"><span data-stu-id="56b45-139">Visual Basic allows a `Yield` statement in the `Try` block of a [Try...Catch...Finally Statement](../../language-reference/statements/try-catch-finally-statement.md).</span></span> <span data-ttu-id="56b45-140">Ein `Try` -Block, der über eine `Yield` -Anweisung verfügt `Catch` , kann-Blöcke aufweisen und kann über einen- `Finally` Block verfügen.</span><span class="sxs-lookup"><span data-stu-id="56b45-140">A `Try` block that has a `Yield` statement can have `Catch` blocks, and can have a `Finally` block.</span></span>

<span data-ttu-id="56b45-141">Das folgende Beispiel enthält `Try` die `Catch` Blöcke, und `Finally` in einer Iteratorfunktion.</span><span class="sxs-lookup"><span data-stu-id="56b45-141">The following example includes `Try`, `Catch`, and `Finally` blocks in an iterator function.</span></span> <span data-ttu-id="56b45-142">Der- `Finally` Block in der Iteratorfunktion wird ausgeführt, bevor die `For Each` Iteration beendet wird.</span><span class="sxs-lookup"><span data-stu-id="56b45-142">The `Finally` block in the iterator function executes before the `For Each` iteration finishes.</span></span>

```vb
Sub Main()
    For Each number As Integer In Test()
        Console.WriteLine(number)
    Next
    Console.WriteLine("For Each is done.")

    ' Output:
    '  3
    '  4
    '  Something happened. Yields are done.
    '  Finally is called.
    '  For Each is done.
    Console.ReadKey()
End Sub

Private Iterator Function Test() As IEnumerable(Of Integer)
    Try
        Yield 3
        Yield 4
        Throw New Exception("Something happened. Yields are done.")
        Yield 5
        Yield 6
    Catch ex As Exception
        Console.WriteLine(ex.Message)
    Finally
        Console.WriteLine("Finally is called.")
    End Try
End Function
```

<span data-ttu-id="56b45-143">Eine- `Yield` Anweisung darf sich nicht in einem- `Catch` Block oder einem- `Finally` Block befinden.</span><span class="sxs-lookup"><span data-stu-id="56b45-143">A `Yield` statement cannot be inside a `Catch` block or a `Finally` block.</span></span>

<span data-ttu-id="56b45-144">Wenn der `For Each` Text (anstelle der Iteratormethode) eine Ausnahme auslöst, wird ein- `Catch` Block in der Iteratorfunktion nicht ausgeführt, aber ein- `Finally` Block in der Iteratorfunktion wird ausgeführt.</span><span class="sxs-lookup"><span data-stu-id="56b45-144">If the `For Each` body (instead of the iterator method) throws an exception, a `Catch` block in the iterator function is not executed, but a `Finally` block in the iterator function is executed.</span></span> <span data-ttu-id="56b45-145">Ein `Catch` Block in einer Iteratorfunktion fängt nur Ausnahmen ab, die innerhalb der Iteratorfunktion auftreten.</span><span class="sxs-lookup"><span data-stu-id="56b45-145">A `Catch` block inside an iterator function catches only exceptions that occur inside the iterator function.</span></span>

## <a name="anonymous-methods"></a><a name="BKMK_AnonymousMethods"></a><span data-ttu-id="56b45-146">Anonyme Methoden</span><span class="sxs-lookup"><span data-stu-id="56b45-146">Anonymous Methods</span></span>

<span data-ttu-id="56b45-147">In Visual Basic kann eine anonyme Funktion eine Iteratorfunktion sein.</span><span class="sxs-lookup"><span data-stu-id="56b45-147">In Visual Basic, an anonymous function can be an iterator function.</span></span> <span data-ttu-id="56b45-148">Dies wird anhand des folgenden Beispiels veranschaulicht.</span><span class="sxs-lookup"><span data-stu-id="56b45-148">The following example illustrates this.</span></span>

```vb
Dim iterateSequence = Iterator Function() _
                      As IEnumerable(Of Integer)
                          Yield 1
                          Yield 2
                      End Function

For Each number As Integer In iterateSequence()
    Console.Write(number & " ")
Next
' Output: 1 2
Console.ReadKey()
```

<span data-ttu-id="56b45-149">Das folgende Beispiel enthält eine nicht-Iteratormethode, mit der die Argumente überprüft werden.</span><span class="sxs-lookup"><span data-stu-id="56b45-149">The following example has a non-iterator method that validates the arguments.</span></span> <span data-ttu-id="56b45-150">Die-Methode gibt das Ergebnis eines anonymen Iterators zurück, der die Auflistungs Elemente beschreibt.</span><span class="sxs-lookup"><span data-stu-id="56b45-150">The method returns the result of an anonymous iterator that describes the collection elements.</span></span>

```vb
Sub Main()
    For Each number As Integer In GetSequence(5, 10)
        Console.Write(number & " ")
    Next
    ' Output: 5 6 7 8 9 10
    Console.ReadKey()
End Sub

Public Function GetSequence(ByVal low As Integer, ByVal high As Integer) _
As IEnumerable
    ' Validate the arguments.
    If low < 1 Then
        Throw New ArgumentException("low is too low")
    End If
    If high > 140 Then
        Throw New ArgumentException("high is too high")
    End If

    ' Return an anonymous iterator function.
    Dim iterateSequence = Iterator Function() As IEnumerable
                              For index = low To high
                                  Yield index
                              Next
                          End Function
    Return iterateSequence()
End Function
```

<span data-ttu-id="56b45-151">Wenn die Validierung stattdessen innerhalb der Iteratorfunktion erfolgt, kann die Überprüfung bis zum Anfang der ersten Iterationen des Texts nicht ausgeführt werden `For Each` .</span><span class="sxs-lookup"><span data-stu-id="56b45-151">If validation is instead inside the iterator function, the validation cannot be performed until the start of the first iteration of the `For Each` body.</span></span>

## <a name="using-iterators-with-a-generic-list"></a><a name="BKMK_GenericList"></a> <span data-ttu-id="56b45-152">Verwenden von Iteratoren mit einer generischen Liste</span><span class="sxs-lookup"><span data-stu-id="56b45-152">Using Iterators with a Generic List</span></span>

<span data-ttu-id="56b45-153">In folgendem Beispiel implementiert die generische Klasse `Stack(Of T)` die generische Schnittstelle <xref:System.Collections.Generic.IEnumerable%601>.</span><span class="sxs-lookup"><span data-stu-id="56b45-153">In the following example, the `Stack(Of T)` generic class implements the <xref:System.Collections.Generic.IEnumerable%601> generic interface.</span></span> <span data-ttu-id="56b45-154">Die Methode `Push` weist Werte an Arrays des Typs `T` zu.</span><span class="sxs-lookup"><span data-stu-id="56b45-154">The `Push` method assigns values to an array of type `T`.</span></span> <span data-ttu-id="56b45-155">Die <xref:System.Collections.Generic.IEnumerable%601.GetEnumerator%2A>-Methode gibt die Arraywerte mit der `Yield`-Anweisung zurück.</span><span class="sxs-lookup"><span data-stu-id="56b45-155">The <xref:System.Collections.Generic.IEnumerable%601.GetEnumerator%2A> method returns the array values by using the `Yield` statement.</span></span>

<span data-ttu-id="56b45-156">Zusätzlich zur generischen Methode <xref:System.Collections.Generic.IEnumerable%601.GetEnumerator%2A> muss auch die nicht generische Methode <xref:System.Collections.IEnumerable.GetEnumerator%2A> implementiert werden.</span><span class="sxs-lookup"><span data-stu-id="56b45-156">In addition to the generic <xref:System.Collections.Generic.IEnumerable%601.GetEnumerator%2A> method, the non-generic <xref:System.Collections.IEnumerable.GetEnumerator%2A> method must also be implemented.</span></span> <span data-ttu-id="56b45-157">Dies liegt daran, dass <xref:System.Collections.Generic.IEnumerable%601> von <xref:System.Collections.IEnumerable> erbt.</span><span class="sxs-lookup"><span data-stu-id="56b45-157">This is because <xref:System.Collections.Generic.IEnumerable%601> inherits from <xref:System.Collections.IEnumerable>.</span></span> <span data-ttu-id="56b45-158">Die nicht generische Implementierung verzögert die generische Implementierung.</span><span class="sxs-lookup"><span data-stu-id="56b45-158">The non-generic implementation defers to the generic implementation.</span></span>

<span data-ttu-id="56b45-159">In diesem Beispiel werden benannte Iteratoren verwendet, um verschiedene Arten der Iteration in der selben Datenauflistung zu unterstützen.</span><span class="sxs-lookup"><span data-stu-id="56b45-159">The example uses named iterators to support various ways of iterating through the same collection of data.</span></span> <span data-ttu-id="56b45-160">Diese benannten Iteratoren sind die Eigenschaften `TopToBottom` und `BottomToTop` und die Methode `TopN`.</span><span class="sxs-lookup"><span data-stu-id="56b45-160">These named iterators are the `TopToBottom` and `BottomToTop` properties, and the `TopN` method.</span></span>

<span data-ttu-id="56b45-161">Die `BottomToTop` Eigenschaften Deklaration enthält das- `Iterator` Schlüsselwort.</span><span class="sxs-lookup"><span data-stu-id="56b45-161">The `BottomToTop` property declaration includes the `Iterator` keyword.</span></span>

```vb
Sub Main()
    Dim theStack As New Stack(Of Integer)

    ' Add items to the stack.
    For number As Integer = 0 To 9
        theStack.Push(number)
    Next

    ' Retrieve items from the stack.
    ' For Each is allowed because theStack implements
    ' IEnumerable(Of Integer).
    For Each number As Integer In theStack
        Console.Write("{0} ", number)
    Next
    Console.WriteLine()
    ' Output: 9 8 7 6 5 4 3 2 1 0

    ' For Each is allowed, because theStack.TopToBottom
    ' returns IEnumerable(Of Integer).
    For Each number As Integer In theStack.TopToBottom
        Console.Write("{0} ", number)
    Next
    Console.WriteLine()
    ' Output: 9 8 7 6 5 4 3 2 1 0

    For Each number As Integer In theStack.BottomToTop
        Console.Write("{0} ", number)
    Next
    Console.WriteLine()
    ' Output: 0 1 2 3 4 5 6 7 8 9

    For Each number As Integer In theStack.TopN(7)
        Console.Write("{0} ", number)
    Next
    Console.WriteLine()
    ' Output: 9 8 7 6 5 4 3

    Console.ReadKey()
End Sub

Public Class Stack(Of T)
    Implements IEnumerable(Of T)

    Private values As T() = New T(99) {}
    Private top As Integer = 0

    Public Sub Push(ByVal t As T)
        values(top) = t
        top = top + 1
    End Sub

    Public Function Pop() As T
        top = top - 1
        Return values(top)
    End Function

    ' This function implements the GetEnumerator method. It allows
    ' an instance of the class to be used in a For Each statement.
    Public Iterator Function GetEnumerator() As IEnumerator(Of T) _
        Implements IEnumerable(Of T).GetEnumerator

        For index As Integer = top - 1 To 0 Step -1
            Yield values(index)
        Next
    End Function

    Public Iterator Function GetEnumerator1() As IEnumerator _
        Implements IEnumerable.GetEnumerator

        Yield GetEnumerator()
    End Function

    Public ReadOnly Property TopToBottom() As IEnumerable(Of T)
        Get
            Return Me
        End Get
    End Property

    Public ReadOnly Iterator Property BottomToTop As IEnumerable(Of T)
        Get
            For index As Integer = 0 To top - 1
                Yield values(index)
            Next
        End Get
    End Property

    Public Iterator Function TopN(ByVal itemsFromTop As Integer) _
        As IEnumerable(Of T)

        ' Return less than itemsFromTop if necessary.
        Dim startIndex As Integer =
            If(itemsFromTop >= top, 0, top - itemsFromTop)

        For index As Integer = top - 1 To startIndex Step -1
            Yield values(index)
        Next
    End Function
End Class
```

## <a name="syntax-information"></a><a name="BKMK_SyntaxInformation"></a><span data-ttu-id="56b45-162">Syntax Informationen</span><span class="sxs-lookup"><span data-stu-id="56b45-162">Syntax Information</span></span>

<span data-ttu-id="56b45-163">Ein Iterator kann als Methode oder als `get`-Zugriffsmethode vorkommen.</span><span class="sxs-lookup"><span data-stu-id="56b45-163">An iterator can occur as a method or `get` accessor.</span></span> <span data-ttu-id="56b45-164">Ein Iterator kann nicht in einem Ereignis, Instanzenkonstruktor, statischen Konstruktor oder statischen Destruktor vorkommen.</span><span class="sxs-lookup"><span data-stu-id="56b45-164">An iterator cannot occur in an event, instance constructor, static constructor, or static destructor.</span></span>

<span data-ttu-id="56b45-165">Es muss eine implizite Konvertierung vom Typ des Ausdrucks in der `Yield`-Anweisung in den Rückgabetyp des Iterators vorhanden sein.</span><span class="sxs-lookup"><span data-stu-id="56b45-165">An implicit conversion must exist from the expression type in the `Yield` statement to the return type of the iterator.</span></span>

<span data-ttu-id="56b45-166">In Visual Basic kann eine Iteratormethode keine Parameter aufweisen `ByRef` .</span><span class="sxs-lookup"><span data-stu-id="56b45-166">In Visual Basic, an iterator method cannot have any `ByRef` parameters.</span></span>

<span data-ttu-id="56b45-167">In Visual Basic ist "yield" kein reserviertes Wort und hat nur dann eine besondere Bedeutung, wenn es in einer `Iterator` Methode oder einem `get` Accessor verwendet wird.</span><span class="sxs-lookup"><span data-stu-id="56b45-167">In Visual Basic, "Yield" is not a reserved word and has special meaning only when it is used in an `Iterator` method or `get` accessor.</span></span>

## <a name="technical-implementation"></a><a name="BKMK_Technical"></a><span data-ttu-id="56b45-168">Technische Implementierung</span><span class="sxs-lookup"><span data-stu-id="56b45-168">Technical Implementation</span></span>

<span data-ttu-id="56b45-169">Auch wenn Sie einen Iterator als Methode schreiben, führt der Compiler für diesen eine Translation in eine geschachtelte Klasse durch, die tatsächlich ein Zustandsautomat ist.</span><span class="sxs-lookup"><span data-stu-id="56b45-169">Although you write an iterator as a method, the compiler translates it into a nested class that is, in effect, a state machine.</span></span> <span data-ttu-id="56b45-170">Diese Klasse überwacht die Position des Iterators solange, wie die `For Each...Next`-Schleife im Clientcode weiter ausgeführt wird.</span><span class="sxs-lookup"><span data-stu-id="56b45-170">This class keeps track of the position of the iterator as long the `For Each...Next` loop in the client code continues.</span></span>

<span data-ttu-id="56b45-171">Um zu sehen, was der Compiler macht, können Sie das Tool Ildasm.exe verwenden, um den Intermediate Language-Code von Microsoft anzuzeigen, der für eine Iteratormethode generiert wird.</span><span class="sxs-lookup"><span data-stu-id="56b45-171">To see what the compiler does, you can use the Ildasm.exe tool to view the Microsoft intermediate language code that is generated for an iterator method.</span></span>

<span data-ttu-id="56b45-172">Wenn Sie einen Iterator für eine [Klasse](../../language-reference/statements/class-statement.md) oder [Struktur](../../language-reference/statements/structure-statement.md)erstellen, müssen Sie nicht die gesamte <xref:System.Collections.IEnumerator> Schnittstelle implementieren.</span><span class="sxs-lookup"><span data-stu-id="56b45-172">When you create an iterator for a [class](../../language-reference/statements/class-statement.md) or [struct](../../language-reference/statements/structure-statement.md), you do not have to implement the whole <xref:System.Collections.IEnumerator> interface.</span></span> <span data-ttu-id="56b45-173">Wenn der Compiler einer Iterator erkennt, generiert er automatisch die Methoden `Current`, `MoveNext` und `Dispose` der <xref:System.Collections.IEnumerator>- und <xref:System.Collections.Generic.IEnumerator%601>-Schnittstelle.</span><span class="sxs-lookup"><span data-stu-id="56b45-173">When the compiler detects the iterator, it automatically generates the `Current`, `MoveNext`, and `Dispose` methods of the <xref:System.Collections.IEnumerator> or <xref:System.Collections.Generic.IEnumerator%601> interface.</span></span>

<span data-ttu-id="56b45-174">In jeder aufeinanderfolgenden Iteration der `For Each…Next`-Schleife (oder im direkten Aufruf von `IEnumerator.MoveNext`) setzt der nächste Iteratorcodetext den Prozess nach der letzten `Yield`-Anweisung fort.</span><span class="sxs-lookup"><span data-stu-id="56b45-174">On each successive iteration of the `For Each…Next` loop (or the direct call to `IEnumerator.MoveNext`), the next iterator code body resumes after the previous `Yield` statement.</span></span> <span data-ttu-id="56b45-175">Anschließend wird die nächste Anweisung fortgesetzt `Yield` , bis das Ende des iteratortexts erreicht ist, oder bis eine- `Exit Function` oder-Anweisung gefunden `Return` wird.</span><span class="sxs-lookup"><span data-stu-id="56b45-175">It then continues to the next `Yield` statement until the end of the iterator body is reached, or until an `Exit Function` or `Return` statement is encountered.</span></span>

<span data-ttu-id="56b45-176">Iteratoren unterstützen die- <xref:System.Collections.IEnumerator.Reset%2A?displayProperty=nameWithType> Methode nicht.</span><span class="sxs-lookup"><span data-stu-id="56b45-176">Iterators do not support the <xref:System.Collections.IEnumerator.Reset%2A?displayProperty=nameWithType> method.</span></span> <span data-ttu-id="56b45-177">Um den Prozess erneut von Anfang an zu durchlaufen, müssen Sie einen neuen Iterator erstellen.</span><span class="sxs-lookup"><span data-stu-id="56b45-177">To re-iterate from the start, you must obtain a new iterator.</span></span>

<span data-ttu-id="56b45-178">Weitere Informationen finden Sie in der [Visual Basic-Sprachspezifikation](../../reference/language-specification/index.md).</span><span class="sxs-lookup"><span data-stu-id="56b45-178">For additional information, see the [Visual Basic Language Specification](../../reference/language-specification/index.md).</span></span>

## <a name="use-of-iterators"></a><a name="BKMK_UseOfIterators"></a><span data-ttu-id="56b45-179">Verwendung von Iteratoren</span><span class="sxs-lookup"><span data-stu-id="56b45-179">Use of Iterators</span></span>

<span data-ttu-id="56b45-180">Mit Iteratoren können Sie die Einfachheit einer `For Each`-Schleife beibehalten, wenn Sie komplexen Code verwenden müssen, um eine Listensequenz aufzufüllen.</span><span class="sxs-lookup"><span data-stu-id="56b45-180">Iterators enable you to maintain the simplicity of a `For Each` loop when you need to use complex code to populate a list sequence.</span></span> <span data-ttu-id="56b45-181">Das kann für Folgende Aktionen nützlich sein:</span><span class="sxs-lookup"><span data-stu-id="56b45-181">This can be useful when you want to do the following:</span></span>

- <span data-ttu-id="56b45-182">Das Modifizieren der Listensequenz nach der ersten Iteration einer `For Each`-Schleife.</span><span class="sxs-lookup"><span data-stu-id="56b45-182">Modify the list sequence after the first `For Each` loop iteration.</span></span>

- <span data-ttu-id="56b45-183">Das Vermeiden des kompletten Ladens einer großen Liste vor der ersten Iteration einer `For Each`-Schleife.</span><span class="sxs-lookup"><span data-stu-id="56b45-183">Avoid fully loading a large list before the first iteration of a `For Each` loop.</span></span> <span data-ttu-id="56b45-184">Ein Beispiel dafür ist das ausgelagerte Abrufen, um einen Batch von Tabellenzeilen zu laden.</span><span class="sxs-lookup"><span data-stu-id="56b45-184">An example is a paged fetch to load a batch of table rows.</span></span> <span data-ttu-id="56b45-185">Ein anderes Beispiel ist die <xref:System.IO.DirectoryInfo.EnumerateFiles%2A>-Methode, die Iteratoren im .NET Framework implementiert.</span><span class="sxs-lookup"><span data-stu-id="56b45-185">Another example is the <xref:System.IO.DirectoryInfo.EnumerateFiles%2A> method, which implements iterators within the .NET Framework.</span></span>

- <span data-ttu-id="56b45-186">Das Einschließen des Erstellens der Liste im Iterator.</span><span class="sxs-lookup"><span data-stu-id="56b45-186">Encapsulate building the list in the iterator.</span></span> <span data-ttu-id="56b45-187">In der Iteratormethode können Sie die Liste erstellen und anschließend jedes Ergebnis in eine Schleife liefern.</span><span class="sxs-lookup"><span data-stu-id="56b45-187">In the iterator method, you can build the list and then yield each result in a loop.</span></span>

## <a name="see-also"></a><span data-ttu-id="56b45-188">Weitere Informationen</span><span class="sxs-lookup"><span data-stu-id="56b45-188">See also</span></span>

- <xref:System.Collections.Generic>
- <xref:System.Collections.Generic.IEnumerable%601>
- [<span data-ttu-id="56b45-189">For Each...Next-Anweisung</span><span class="sxs-lookup"><span data-stu-id="56b45-189">For Each...Next Statement</span></span>](../../language-reference/statements/for-each-next-statement.md)
- [<span data-ttu-id="56b45-190">Yield-Anweisung</span><span class="sxs-lookup"><span data-stu-id="56b45-190">Yield Statement</span></span>](../../language-reference/statements/yield-statement.md)
- [<span data-ttu-id="56b45-191">Iterator</span><span class="sxs-lookup"><span data-stu-id="56b45-191">Iterator</span></span>](../../language-reference/modifiers/iterator.md)
